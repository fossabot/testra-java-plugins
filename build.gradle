buildscript {
    ext {
        dependencyManagementPluginVersion = '1.0.3.RELEASE'
        js2pPluginVersion = '0.4.33'
    }
    repositories {
        jcenter()
        maven { url 'https://projectlombok.org/edge-releases' }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:${dependencyManagementPluginVersion}")
        classpath "org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:${js2pPluginVersion}"
        classpath "org.projectlombok:lombok:edge-SNAPSHOT"
        classpath "gradle.plugin.com.energizedwork.webdriver-binaries:webdriver-binaries-gradle-plugin:1.2"
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.8.3'
    id 'com.jfrog.artifactory' version '4.7.5'
    id 'maven-publish'
}

// Dependency versions
ext.logbackVersion = '1.1.2'
ext.slf4jVersion = '1.7.2'
ext.junitVersion = '4.12'
ext.jacksonVersion = '2.8.8'
ext.apacheCommondLangVersion = '3.5'
ext.requestBuilderVersion = '0.0.1'
ext.cucumberVersion = '1.2.5'
ext.assertjVersion = '1.0.0m1'
ext.testngVersion = '6.11'
ext.okHttpLoggingVersion = '1.0.1'
ext.fluentLeniumVersion = '3.5.0'
ext.seleniumVersion = '3.8.1'

allprojects {
    group 'tech.testra'
    version '0.19'

    apply plugin: 'idea'
    apply plugin: 'project-report'

    repositories {
        mavenLocal()
        maven { url 'https://projectlombok.org/edge-releases' }
        jcenter()
    }

    idea {
        module {
            outputDir file('build/classes/main')
            testOutputDir file('build/classes/test')
        }
    }
    if (project.convention.findPlugin(JavaPluginConvention)) {
        sourceSets.main.output.classesDir = new File(buildDir, "classes/main")
        sourceSets.main.output.resourcesDir = new File(buildDir, "resources/main")
        sourceSets.test.output.classesDir = new File(buildDir, "classes/test")
        sourceSets.test.output.resourcesDir = new File(buildDir, "resources/test")
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.jfrog.artifactory'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencyManagement {
        dependencies {
            // External dependencies
            dependency group: 'ch.qos.logback', name: 'logback-classic', version: "${logbackVersion}"
            dependency group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"
            dependency group: 'org.testng', name: 'testng', version: "${testngVersion}"
            dependency group: 'junit', name: 'junit', version: "${junitVersion}"
            dependency group: 'org.apache.commons', name: 'commons-lang3', version: "${apacheCommondLangVersion}"
            dependency group: 'info.cukes', name: 'cucumber-jvm', version: "${cucumberVersion}"
            dependency group: 'info.cukes', name: 'cucumber-java', version: "${cucumberVersion}"
            dependency group: 'info.cukes', name: 'cucumber-picocontainer', version: "${cucumberVersion}"
            dependency group: 'info.cukes', name: 'cucumber-testng', version: "${cucumberVersion}"
            dependency group: 'org.assertj', name: 'assertj-core-java8', version: "${assertjVersion}"
            dependency group: 'com.jleth.util', name: 'okhttp2-logging', version: "${okHttpLoggingVersion}"
            dependency group: 'org.seleniumhq.selenium', name: 'selenium-java', version: seleniumVersion
            dependency group: 'com.google.guava', name: 'guava', version: '24.0-jre'
            dependency group: 'org.mockito', name: 'mockito-core', version: '2.19.0'
            dependency group: 'ch.qos.logback', name:'logback-classic', version: '1.0.9'
            dependency group: 'ch.qos.logback', name:'logback-core', version: '1.0.9'
            dependency group: 'org.jbehave', name:'jbehave-core' , version: '4.3.4'
            dependency group: 'org.projectlombok', name: 'lombok', version: 'edge-SNAPSHOT'
            dependency group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.14.0'


        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    javadoc.failOnError = false
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
//        repositories {
//            maven {
//                credentials {
//                    username 'pajohnson'
//                    password ''
//                }
//                // change to point to your repo, e.g. http://my.org/repo
//                url = "https://nexus.dtc.prod.williamhill.plc:8443/repository/3rd-party/"
//
//            }
//        }
        publications {
            mavenPublication(MavenPublication) {
                from components.java
                artifact sourcesJar {
                    classifier "sources"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }
                pom {
                    url = 'https://github.com/testra-tech/testra-java-plugins'
                    licenses {
                        license {
                            name = 'GNU GENERAL PUBLIC LICENSE, Version 3.0'
                            url = 'https://www.gnu.org/licenses/gpl.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'paulsjohnson91'
                            name = 'Paul Johnson'
                        }
                        developer {
                            id = 'sudokar'
                            name = 'sudokar'
                        }
                    }
                    scm {
                        url = 'https://github.com/testra-tech/testra-java-plugins'
                    }
                }
            }
        }
    }

    artifactory {
        contextUrl = 'http://oss.jfrog.org'
        publish {
            repository {
                repoKey = 'oss-snapshot-local'
                username = System.getProperty('bintray.user')
                password = System.getProperty('bintray.key')
            }
            defaults {
                publications('mavenPublication')
                publishArtifacts = true
                publishPom = true
            }
        }
        resolve {
            repoKey = 'jcenter'
        }
        clientConfig.info.setBuildNumber(System.getProperty('build.number'))
    }

    bintray {
        user = System.getProperty('bintray.user')
        key = System.getProperty('bintray.key')
        publications = ['mavenPublication']
        override = true
        dryRun = false
        publish = true
        pkg {
            repo = 'testra'
            name = "$project.name"
            publicDownloadNumbers = true
            desc = 'Testra plugins for Java'
            licenses = ['GPL-3.0']
            vcsUrl = 'https://github.com/testra-tech/testra-java-plugins'
            version {
                name = "$project.version"
                desc = "$project.version"
                released  = new Date()
            }
        }
    }
}

configure(subprojects
        .findAll { it.name == 'testra-java-cucumberv2-plugin' || it.name == 'testra-java-cucumberv1-plugin'|| it.name == 'testra-java-cucumberv3-plugin' || it.name == 'testra-java-jbehave-plugin'}) {
    apply plugin: 'com.energizedwork.webdriver-binaries'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}